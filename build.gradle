allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    version = '0.1-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
}

subprojects {
    dependencies {
        compile 'org.slf4j:slf4j-api:1.7.7'
        compile 'ch.qos.logback:logback-classic:1.1.3'
        testCompile 'junit:junit:4.11'
    }

    test.useTestNG()
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}